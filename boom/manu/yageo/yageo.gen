#GEN

# http://www.yageo.com/documents/recent/Product Selection Guide 2009.pdf
# http://www.yageo.com/documents/recent/PYu-RC0402_51_RoHS_L_5.pdf

RC* -> T=R {
    RC(????)([FJ])[RK]-(07|10|13)(*)L* ->
	FP=$REF:1 _TOL=$REF:2 _R=$REF:4

    include ../../lib/tol.inc

    FP=0100 -> FP=01005 P=1/32W V=15
    FP=0201 -> P=1/20W V=25V
    FP=0402 -> P=1/16W V=50V
    FP=0603 -> P=1/10W V=50V
    FP=0805 -> P=1/8W V=150V
    FP=1206 -> P=1/4W V=200V

    # Adjust multipliers

    _R=(*)R(*)	->	_R=0$_R:1.$_R:2
    _R=(*)K(*)	->	_R=$_R:1.${_R:2}k
    _R=(*)M(*)	->	_R=$_R:1.${_R:2}M

    # Remove leading and trailing zeroes

    _R=0([0-9]*)		-> _R=$_R:1
    _R=(*.[0-9]*)0([kM])	-> _R=$_R:1$_R:2
    _R=(*).0([kM])		-> _R=$_R:1$_R:2
    _R=(*.[0-9]*)0		-> _R=$_R:1
    _R=(*).0			-> _R=$_R:1
    _R=(*).([kM]|)		-> _R=$_R:1$_R:2
}

# http://www.yageo.com/documents/recent/UPY-GPHC_Y5V_6.3V-to-50V_5.pdf

CC* -> T=C {
    CC(????)(?)?(???)(?)??([0-9R][0-9R][0-9]) ->
      FP=$REF:1 _TOL=$REF:2 M=$REF:3 _V=$REF:4 _E12_P=$REF:5

    include ../../lib/captol.inc

    _V=5 -> V=6.3V
    _V=6 -> V=10V
    _V=7 -> V=16V
    _V=8 -> V=25V
    _V=9 -> V=50V
    _V=B -> V=500V
    _V=C -> V=1kV
    _V=E -> V=3kV
}

include ../../lib/e12.inc

T=R -> R=${_R}R !
T=C -> C=${_E12}F !
